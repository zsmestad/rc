# [Antigen](https://github.com/zsh-users/antigen)
# [Font](https://github.com/ryanoasis/nerd-fonts)

case `uname` in
  Linux)
    is_linux=true;;
  Darwin)
    is_mac=true;;
  *)
    platform='unknown';;
esac

source ~/.antigen/antigen.zsh
antigen use oh-my-zsh

[ $is_mac ] && antigen bundle osx

antigen bundle direnv

# Python
antigen bundle pyenv
antigen bundle pip

# Ruby
if [ -x ~/.rvm/bin/rvm ]; then
  antigen bundle rvm
elif [ -x /usr/local/bin/rbenv ]; then
  antigen bundle rbenv
fi
antigen bundle rake-fast

# Completions
antigen bundle cargo
antigen bundle docker
antigen bundle fd

antigen apply

autoload -U colors && colors

export TERM='xterm-256color'

set -o vi

###################
# Shell Variables #

# Need pyenv and rbenv in front of these
[ -d ~/.local/bin ] && export PATH=$PATH:~/.local/bin
PATH=$PATH:~/bin:/usr/local/bin:/usr/local/sbin

# Localization
LANG='en_US.UTF-8'
LC_ALL='en_US.UTF-8'
export PYTHONIOENCODING=utf-8

# History
HISTFILE=~/.zsh_history
SAVEHIST=5000
setopt HIST_IGNORE_DUPS

# Prefer nvim
nv="$(which nvim)"
if [ $? -eq 0 ]; then
  alias vi="$nv"
  alias vim="$nv"
  alias vimdiff="$nv -d"
  export EDITOR="$nv"
else
  export EDITOR="$(which vim)"
fi
unset nv

# Env Vars
export PAGER="/usr/bin/less -Rs"
export VISUAL="$EDITOR"
export LESSEDIT="$EDITOR"
export LESSCHARSET='utf-8'
export LESS='--ignore-case -R'

#########
# Alias #
if [ $is_mac ]; then
  alias ls='ls -G'
else
  alias ls='ls --color=auto --time-style=long-iso'
fi

alias  l='ls -lF'
alias la='l -A'
alias lh='l -h'

alias mp='mkdir -p'

alias   ..='cd ..'
alias  ...='cd ../..'
alias ....='cd ../../..'

alias cl='clear'

alias add_cwd_to_path='export PATH=`pwd`:$PATH'

# Kubernetes
alias kc=kubectl
alias kx=kubectx
alias kn=kubens

##############
# Completion #
autoload -U compinit && compinit
# Case insensitive completion
zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=*'
zstyle ':completion:*' completer _complete
zstyle ':completion:*' group-name ''
zstyle ':completion:*' list-colors ''
zstyle ':completion:*' use-compctl false

if [ -f ~/.ssh/known_hosts ]; then
  local knownhosts
  knownhosts=( ${${${${(f)"$(<$HOME/.ssh/known_hosts)"}:#[0-9]*}%%\ *}%%,*} )
  zstyle ':completion:*:(ssh|scp|sftp):*' hosts $knownhosts
fi

hfunc=~/.zsh/func
[ -d $hfunc ] && fpath+=$hfunc

ufunc=/usr/local/share/zsh/site-functions
[ -d $ufunc ] && fpath+=$ufunc
unset hfunc ufunc

# Attach unattached tmux session or create a new one
function tt () {
  n=$(tmux list-sessions 2> /dev/null | grep -Ev '\(attached\)$' | wc -l | tr -d '[:blank:]')
  if [ $n -gt 0 ]; then
    tmux attach $@
  else
    tmux $@
  fi
}

# Man in Preview
# https://news.ycombinator.com/item?id=25308708
if [ $is_mac ]; then
  pman() { man -t "$@" | open -f -a Preview; }
fi

## Machine specific zshrc
zsh_local=~/.zshrc.local
[ -f $zsh_local ] && source $zsh_local
unset zsh_local

################
# App Specific #

# Kubernetes
[ "$(which kubectl 2> /dev/null)" ] && eval "$(kubectl completion zsh)"

# https://github.com/junegunn/fzf
[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

## Rust
# https://rustup.rs/
cargo_bin=~/.cargo/bin
[ -d "$cargo_bin" ] && export PATH="${cargo_bin}:$PATH"
unset cargo_bin

# https://starship.rs
[ "$(which starship 2> /dev/null)" ] && eval "$(starship init zsh)"

# https://github.com/ajeetdsouza/zoxide/
zoxide_bin=~/.cargo/bin/zoxide
[ "$(which zoxide 2> /dev/null)" ] && eval "$(zoxide init zsh)"

# https://crates.io/crates/sccache
[ "$(which sccache 2> /dev/null)" ] && export RUSTC_WRAPPER=sccache

[ -f ~/.config/ripgrep/conf ] && export RIPGREP_CONFIG_PATH=~/.config/ripgrep/conf

# Remove dupes
typeset -U PATH
typeset -U fpath
