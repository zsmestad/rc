# [Antigen](https://github.com/zsh-users/antigen)
# [Font](https://github.com/ryanoasis/nerd-fonts)

case `uname` in
  Linux)
    is_linux=true;;
  Darwin)
    is_mac=true;;
  *)
    platform='unknown';;
esac

source ~/.antigen/antigen.zsh
antigen use oh-my-zsh

[ $is_mac ] && \
  antigen bundle osx

#antigen bundle autojump

# Python
antigen bundle pyenv
antigen bundle pip

# Ruby
antigen bundle rbenv
antigen bundle rake-fast

# Completions
antigen bundle cargo
antigen bundle vagrant
antigen bundle docker

antigen apply

autoload -U colors && colors

export TERM='xterm-256color'

set -o vi

###################
# Shell Variables #

# Need pyenv and rbenv in front of these
[ -d ~/.local/bin ] && export PATH=$PATH:~/.local/bin
PATH=$PATH:~/bin:/usr/local/bin:/usr/local/sbin

# Localization
LANG='en_US.UTF-8'
LC_ALL='en_US.UTF-8'
export PYTHONIOENCODING=utf-8

# History
HISTFILE=~/.zsh_history
SAVEHIST=1000
setopt HIST_IGNORE_DUPS

# Prefer nvim
nv="$(which nvim)"
if [ -x "$nv" ]; then
  alias vi="$nv"
  alias vim="$nv"
  alias vimdiff="$nv -d"
  export EDITOR="$nv"
else
  export EDITOR="$(which vim)"
fi

# Env Vars
export PAGER="/usr/bin/less -Rs"
export VISUAL="$EDITOR"
export LESSEDIT="$EDITOR"
export LESSCHARSET='utf-8'
export LESS='--ignore-case -R'

#########
# Alias #
if [ $is_mac ]; then
  alias ls='ls -G'
else
  alias ls='ls --color=auto --time-style=long-iso'
fi

alias  l='ls -lF'
alias la='l -A'
alias lh='l -h'

alias mp='mkdir -p'

alias   ..='cd ..'
alias  ...='cd ../..'
alias ....='cd ../../..'

alias cl='clear'

alias pl='puppet-lint --log-format "%{filename}:%{line} %{KIND} - %{message}" --no-autoloader_layout-check'
alias pp='puppet parser validate'

alias add_cwd_to_path='export PATH=`pwd`:$PATH'

##############
# Completion #
autoload -U compinit && compinit
# Case insensitive completion
zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=*'
zstyle ':completion:*' completer _complete
zstyle ':completion:*' group-name ''
zstyle ':completion:*' list-colors ''
zstyle ':completion:*' use-compctl false

if [ -f ~/.ssh/known_hosts ]; then
  local knownhosts
  knownhosts=( ${${${${(f)"$(<$HOME/.ssh/known_hosts)"}:#[0-9]*}%%\ *}%%,*} )
  zstyle ':completion:*:(ssh|scp|sftp):*' hosts $knownhosts
fi

hfunc=~/.zsh/func
[ -d $hfunc ] && fpath+=$hfunc

ufunc=/usr/local/share/zsh/site-functions
[ -d $ufunc ] && fpath+=$ufunc

# Attach unattached tmux session or create a new one
function tt () {
  n=$(tmux list-sessions 2> /dev/null | grep -Ev '\(attached\)$' | wc -l | tr -d '[:blank:]')
  if [ $n -gt 0 ]; then
    tmux attach $@
  else
    tmux $@
  fi
}

# Man in Preview
# https://news.ycombinator.com/item?id=25308708
if [ $is_mac ]; then
  pman() { man -t "$@" | open -f -a Preview; }
fi

## Machine specific zshrc
zsh_local=~/.zshrc.local
[ -f $zsh_local ] && source $zsh_local

# https://github.com/junegunn/fzf
[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

## Rust
# https://rustup.rs/
rustup_bin=~/.cargo/bin
[ -d "$rustup_bin" ] && export PATH="${rustup_bin}:$PATH"

# https://starship.rs
starship_bin=~/.cargo/bin/starship
[ -x "$starship_bin" ] && eval "$($starship_bin init zsh)"

# https://github.com/ajeetdsouza/zoxide/
zoxide_bin=~/.cargo/bin/zoxide
[ -x "$zoxide_bin" ] && eval "$(zoxide init zsh)"

# https://crates.io/crates/sccache
[ -x ~/.cargo/bin/sccache ] && export RUSTC_WRAPPER=sccache

[ -f ~/.config/ripgrep/conf ] && export RIPGREP_CONFIG_PATH=~/.config/ripgrep/conf

unset hfunc ufunc zsh_local rustup_bin starship_bin zoxide_bin nv

# Remove dupes
typeset -U PATH
typeset -U fpath
